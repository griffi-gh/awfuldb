WHITESPACE = _{ (WHITE_SPACE)+ }
COMMENT = _{
  ("/*" ~ (!"*/" ~ ANY)* ~ "*/") |
  ("//" ~ (!(NEWLINE | EOI) ~ ANY)* ~ (NEWLINE| EOI))
}

numeric = _{ ASCII_DIGIT+ }
item = { (ASCII_ALPHANUMERIC | "_")+ }

nil_literal = { "nil" }
bool_literal = { "true" | "false" }
number_literal = { numeric }
string_literal = { "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
literal = _{ number_literal | bool_literal | string_literal | nil_literal }

//unused:
cmp_gt = { ">" }
cmp_gt_eq = { ">=" }
cmp_lt = { "<" }
cmp_lt_eq = { "<=" }
cmp_eq = { "==" }
cmp_not_eq = { "!=" }
cmp = _{ cmp_gt | cmp_gt_eq | cmp_lt | cmp_lt_eq | cmp_eq | cmp_not_eq }

//TODO: proper expressions
expr = { literal }

expr_list = { "(" ~ expr? ~ ("," ~ expr?)* ~ ")" }

expr_table_item = { item ~ ":" ~ expr }
expr_table = { "{" ~ expr_table_item? ~ ("," ~ expr_table_item?)* ~ "}" }

size = { "[" ~ expr ~ "]" }
type = {
  "u8" | "u16" | "u32" | "u64" |
  "i8" | "i16" | "i32" | "i64" |
  "f32" | "f64" | "text" | "blob"
}

// CreateTable test {
// 	name: text[10],
// 	age: u16,
// };
column_type = { type ~ size? ~ "?"? }
column_def = { item ~ ":" ~ column_type }
column_def_list = { ("{" ~ column_def? ~ ("," ~ column_def?)* ~ "}")* }
create_table = { "CreateTable" ~ item ~ column_def_list? }

// DropTable test;
drop_table = { "DropTable" ~ item }

// InsertInto test {
//   name: "Adam",
//   age: 17,
// };
// InsertInto test ("Adam", 17);
insert_into = { "InsertInto" ~ item ~ (expr_list | expr_table) }

statement = { (create_table | drop_table | insert_into) ~ ";" }

query = { SOI ~ statement* ~ EOI }
